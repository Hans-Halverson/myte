module myte.parse.ast

import myte.common.loc.Loc

pub type Module {
  loc: Loc,
  name: ModuleName,
  imports: Vec<Import>,
  toplevels: Vec<Toplevel>,
  source: String,
}

pub type ModuleName {
  loc: Loc,
  name: ScopedIdentifier,
}

pub type Import =
  | ISimpleImport (ScopedIdentifier)
  | IComplexImport (ComplexImport)

pub type ComplexImport {
  loc: Loc,
  scopes: Vec<Identifier>,
  aliases: Vec<ImportAlias>,
}

pub type ImportAlias {
  loc: Loc,
  name: Identifier,
  alias_: Option<Identifier>,
}

pub type Toplevel =
  | TLFunction (FunctionDeclaration)
  | TLVariable (VariableDeclaration)
  | TLType (TypeDeclaration)
  | TLTrait (TraitDeclaration)

pub type Identifier {
  loc: Loc,
  name: String,
}

pub type ScopedIdentifier {
  loc: Loc,
  scopes: Vec<Identifier>,
  name: Identifier,
}

/* 
 * ============================
 *        Declarations
 * ============================
 */

pub type FunctionDeclaration {
  loc: Loc,
  name: Identifier,
  params: Vec<FunctionParam>,
  body: FunctionBody,
  return_: Option<Type>,
  typeParams: Vec<TypeParameter>,
  isPublic: Bool,
  isBuiltin: Bool,
  isStatic: Bool,
  isOverride: Bool,
}

pub type FunctionParam {
  loc: Loc,
  name: Identifier,
  annot: Type,
}

pub type FunctionBody =
  | FBBlock (Block)
  | FBExpression (Expression)
  | FBSignature

pub type TypeParameter {
  loc: Loc,
  name: Identifier,
  bounds: Vec<IdentifierType>,
}

pub type VariableDeclaration {
  loc: Loc,
  kind: VariableDeclarationKind,
  pattern: Pattern,
  init: Expression,
  annot: Option<Type>,
  isPublic: Bool,
}

pub type VariableDeclarationKind =
  | Immutable
  | Mutable

pub type TypeDeclaration {
  loc: Loc,
  name: Identifier,
  typeParams: Vec<TypeParameter>,
  decl: TypeDecl,
  isPublic: Bool,
}

pub type TypeDecl =
  | TDAlias (Type)
  | TDTuple (TupleDeclaration)
  | TDRecord (RecordDeclaration)
  | TDVariant (Vec<VariantDeclaration>)
  | TDBuiltin

pub type TupleDeclaration {
  loc: Loc,
  name: Identifier,
  elements: Vec<Type>,
}

pub type RecordDeclaration {
  loc: Loc,
  name: Identifier,
  fields: Vec<RecordDeclarationField>,
}

pub type RecordDeclarationField {
  loc: Loc,
  name: Identifier,
  type_: Type,
  isPublic: Bool,
  isMutable: Bool,
}

pub type VariantDeclaration =
  | TupleVariant (TupleDeclaration)
  | RecordVariant (RecordDeclaration)
  | EnumVariant (Identifier)

pub type TraitDeclaration {
  loc: Loc,
  kind: TraitDeclarationKind,
  name: Identifier,
  typeParams: Vec<TypeParameter>,
  implemented: Vec<IdentifierType>,
  methods_: Vec<FunctionDeclaration>,
  isPublic: Bool,
}

pub type TraitDeclarationKind =
  | TKMethods
  | TKTrait

/* 
 * ============================
 *         Statements
 * ============================
 */

pub type Statement =
  | SBlock (Block)
  | SExpression (ExpressionStatement)
  | SVariableDeclaration (VariableDeclaration)
  | SIf (If)
  | SMatch (Match)
  | SWhile (While)
  | SFor (For)
  | SReturn (Return)
  | SBreak (Break)
  | SContinue (Continue)
  | SAssignment (Assignment)

pub type Block {
  loc: Loc,
  statements: Vec<Statement>,
}

pub type ExpressionStatement {
  loc: Loc,
  expr: Expression,
  isValue: Bool,
}

pub type If {
  loc: Loc,
  test: Test,
  conseq: Block,
  altern: IfAltern,
}

pub type IfAltern =
  | Else (Block)
  | ElseIf (If)
  | NoAltern

pub type Test =
  | TExpression (Expression)
  | TMatch (MatchTest)

pub type MatchTest {
  loc: Loc,
  expr: Expression,
  pattern: Pattern,
  guard: Option<Expression>,
}

pub type Match {
  loc: Loc,
  args: Vec<Expression>,
  cases: Vec<MatchCase>,
}

pub type MatchCase {
  loc: Loc,
  pattern: Pattern,
  guard: Option<Expression>,
  body: MatchCaseBody,
}

pub type MatchCaseBody =
  | MCExpression (Expression)
  | MCStatement (Statement)

pub type While {
  loc: Loc,
  test: Test,
  body: Block,
}

pub type For {
  loc: Loc,
  pattern: Pattern,
  annot: Option<Type>,
  iterator: Expression,
  body: Block,
}

pub type Return {
  loc: Loc,
  arg: Option<Expression>,
}

pub type Break { loc: Loc }

pub type Continue { loc: Loc }

pub type Assignment {
  loc: Loc,
  op: Option<AssignmentOperation>,
  lvalue: AssignmentLvalue,
  expr: Expression,
}

pub type AssignmentOperation =
  | AssignAdd
  | AssignSubtract
  | AssignMultiply
  | AssignDivide
  | AssignRemainder
  | AssignBitwiseAnd
  | AssignBitwiseOr
  | AssignBitwiseXor
  | AssignLeftShift
  | AssignArithmeticRightShift
  | AssignLogicalRightShift

pub type AssignmentLvalue =
  | ALPattern (Pattern)
  | ALExpression (Expression)

/* 
 * ============================
 *        Expressions
 * ============================
 */

pub type Expression =
  | EUnitLiteral (UnitLiteral)
  | EIntLiteral (IntLiteral)
  | EFloatLiteral (FloatLiteral)
  | EBoolLiteral (BoolLiteral)
  | EStringLiteral (StringLiteral)
  | ECharLiteral (CharLiteral)
  | EInterpolatedString (InterpolatedString)
  | EIdentifier (Identifier)
  | EScopedIdentifier (ScopedIdentifier)
  | ETuple (TupleExpression)
  | ERecord (RecordExpression)
  | ETypeCast (TypeCast)
  | ECall (Call)
  | EUnaryOperation (UnaryOperation)
  | EBinaryOperation (BinaryOperation)
  | ELogicalAnd (LogicalAnd)
  | ELogicalOr (LogicalOr)
  | ENamedAccess (NamedAccess)
  | EIndexedAccess (IndexedAccess)
  | EUnwrap (Unwrap)
  | EIf (If)
  | EMatch (Match)
  | EVecLiteral (VecLiteral)
  | EMapLiteral (MapLiteral)
  | ESetLiteral (SetLiteral)
  | EAnonymousFunction (AnonymousFunction)

pub type UnitLiteral { loc: Loc }

pub type IntLiteral {
  loc: Loc,
  raw: String,
  base: IntLiteralBase,
}

pub type FloatLiteral {
  loc: Loc,
  raw: String,
}

pub type IntLiteralBase =
  | Dec
  | Bin
  | Hex

pub type BoolLiteral {
  loc: Loc,
  value: Bool,
}

pub type StringLiteral {
  loc: Loc,
  value: String,
}

pub type CharLiteral {
  loc: Loc,
  value: Byte,
}

pub type InterpolatedString {
  loc: Loc,
  parts: Vec<InterpolatedStringPart>,
}

pub type InterpolatedStringPart =
  | IPString (StringLiteral)
  | IPExpression (Expression)

pub type TupleExpression {
  loc: Loc,
  elements: Vec<Expression>,
}

pub type RecordExpression {
  loc: Loc,
  name: Expression,
  fields: Vec<RecordExpressionField>,
  // Needed for reparsing expressions as patterns. Record expressions cannot have a `...`.
  rest: Option<Loc>,
}

pub type RecordExpressionField {
  loc: Loc,
  name: Identifier,
  value: Option<Expression>,
}

pub type TypeCast {
  loc: Loc,
  expr: Expression,
  type_: Type,
}

pub type Call {
  loc: Loc,
  func: Expression,
  args: Vec<Expression>,
}

pub type UnaryOperation {
  loc: Loc,
  op: UnaryOperator,
  operand: Expression,
}

pub type UnaryOperator =
  | UnaryPlus
  | UnaryMinus
  | UnaryNot

pub type BinaryOperation {
  loc: Loc,
  op: BinaryOperator,
  left: Expression,
  right: Expression,
}

pub type BinaryOperator =
  | BinaryAdd
  | BinarySubtract
  | BinaryMultiply
  | BinaryDivide
  | BinaryRemainder
  | BinaryEqual
  | BinaryNotEqual
  | BinaryLessThan
  | BinaryGreaterThan
  | BinaryLessThanOrEqual
  | BinaryGreaterThanOrEqual
  | BinaryBitwiseAnd
  | BinaryBitwiseOr
  | BinaryBitwiseXor
  | BinaryLeftShift
  | BinaryArithmeticRightShift
  | BinaryLogicalRightShift

pub type LogicalAnd {
  loc: Loc,
  left: Expression,
  right: Expression,
}

pub type LogicalOr {
  loc: Loc,
  left: Expression,
  right: Expression,
}

pub type NamedAccess {
  loc: Loc,
  target: Expression,
  name: Identifier,
}

pub type IndexedAccess {
  loc: Loc,
  target: Expression,
  index: Expression,
}

pub type Unwrap {
  loc: Loc,
  operand: Expression,
}

pub type VecLiteral {
  loc: Loc,
  elements: Vec<Expression>,
}

pub type MapLiteral {
  loc: Loc,
  entries: Vec<MapLiteralEntry>,
}

pub type MapLiteralEntry {
  loc: Loc,
  key: Expression,
  value: Expression,
}

pub type SetLiteral {
  loc: Loc,
  elements: Vec<Expression>,
}

pub type AnonymousFunction {
  loc: Loc,
  params: Vec<AnonymousFunctionParam>,
  return_: Option<Type>,
  body: AnonymousFunctionBody,
}

pub type AnonymousFunctionParam {
  loc: Loc,
  name: Identifier,
  annot: Option<Type>,
}

pub type AnonymousFunctionBody =
  | ABBlock (Block)
  | ABExpression (Expression)

/* 
 * ============================
 *           Types
 * ============================
 */

pub type Type =
  | TIdentifier (IdentifierType)
  | TTuple (TupleType)
  | TFunction (FunctionType)
  | TTrait (TraitType)

pub type IdentifierType {
  loc: Loc,
  name: ScopedIdentifier,
  typeArgs: Vec<Type>,
}

pub type TupleType {
  loc: Loc,
  elements: Vec<Type>,
}

pub type FunctionType {
  loc: Loc,
  params: Vec<Type>,
  return_: Type,
}

pub type TraitType {
  loc: Loc,
  trait_: IdentifierType,
}

/* 
 * ============================
 *          Patterns
 * ============================
 */

pub type Pattern =
  | PIdentifier (ScopedIdentifier)
  | PWildcard (Loc)
  | PNamedWildcard (NamedWildcard)
  | PBinding (BindingPattern)
  | POr (OrPattern)
  | PTuple (TuplePattern)
  | PRecord (RecordPattern)
  | PLiteral (LiteralPattern)

pub type LiteralPattern =
  | PUnit (UnitLiteral)
  | PBool (BoolLiteral)
  | PInt (IntLiteral)
  | PChar (CharLiteral)
  | PString (StringLiteral)

pub type NamedWildcard {
  loc: Loc,
  name: ScopedIdentifier,
}

pub type OrPattern {
  loc: Loc,
  left: Pattern,
  right: Pattern,
}

pub type TuplePattern {
  loc: Loc,
  name: Option<ScopedIdentifier>,
  elements: Vec<Pattern>,
}

pub type RecordPattern {
  loc: Loc,
  name: ScopedIdentifier,
  fields: Vec<RecordPatternField>,
  rest: Bool,
}

pub type RecordPatternField {
  loc: Loc,
  name: Option<Identifier>,
  value: Pattern,
}

pub type BindingPattern {
  loc: Loc,
  pattern: Pattern,
  name: Identifier,
}
