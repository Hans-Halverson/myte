module test

import std.io.{STDIN, println}
import std.io.file.File
import std.io.read.BufferedReader
import std.io.write.BufferedWriter
import std.sys.args

fun copyFile(srcFile: File, destFile: File): Int {
  val reader = BufferedReader.new(srcFile);
  val writer = BufferedWriter.new(destFile);

  for (line in reader.lines()) {
    match (writer.writeString(line)) {
      | Error _ -> return 1
      | _ -> {}
    }

    match (writer.writeByte('\n')) {
      | Error _ -> return 1
      | _ -> {}
    }
  }

  return match (writer.flush()) {
    | Ok _ -> 0
    | Error _ -> 1
  };
}

fun main(): Int {
  if (args.size == 1) {
    println("Error: Requires at least one argument");

    return 1;
  } else if (args.size == 2) {
    return match (File.create(args[1])) {
      | Ok (destFile) -> copyFile(STDIN, destFile)
      | _ -> 1
    };
  } else {
    return match (File.open(args[1]), File.create(args[2])) {
      | (Ok (srcFile), Ok (destFile)) -> copyFile(srcFile, destFile)
      | _ -> 1
    };
  }
}