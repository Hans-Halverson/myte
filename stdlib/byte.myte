module std.byte

import std.bool.Bool
import std.hash.Hashable
import std.int.Int
import std.long.Long
import std.ops.Equatable
import std.string.{String, ToString}

builtin type Byte

val BYTE_MIN: Byte = -128

val BYTE_MAX: Byte = 127

methods Byte {
  implements Equatable

  builtin override fun equals(other: Byte): Bool
}

methods Byte {
  implements Hashable

  override fun hash(): Int = this.toInt()
}

methods Byte {
  implements ToString

  builtin fun toInt(): Int

  builtin fun toLong(): Long

  override fun toString(): String {
    var n = this;
    // Special case min byte, as it cannot be negated
    if (n == BYTE_MIN) {
      return "-128";
    }

    val str = String.new();
    val isNegative = n < 0;
    if (isNegative) {
      n = -n;
    }

    // Add digits one at a time to string
    while (n > 9) {
      str.push((n % 10) + 48);
      n = n / 10;
    }

    // Add last digit to string
    str.push(n + 48);
    
    // Add negative sign
    if (isNegative) {
      str.push(45);
    }

    str.reverse();

    return str;
  }
}