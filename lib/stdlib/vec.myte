module std.vec

import std.bool.Bool
import std.int.Int
import std.iterator.{Iterable, Iterator}
import std.memory.Array
import std.option.{None, Option, Some}
import std.sys.abort

/*
 * A resizable container of elements.
 */
type Vec<T> {
  var data: Array<T>,
  var size: Int,
  var capacity: Int,
}

methods Vec<T> {
  static fun new<T>(): Vec<T> {
    return Vec {
      data: Array.new(0),
      size: 0,
      capacity: 0,
    };
  }

  static fun newUninitialized<T>(size: Int): Vec<T> {
    return Vec {
      data: Array.new(size),
      size,
      capacity: size,
    };
  }

  fun get(index: Int): T {
    if (index < 0 || index >= this.size) {
      abort();
    }

    return this.data[index];
  }

  fun set(index: Int, item: T) {
    if (index < 0 || index >= this.size) {
      abort();
    }

    this.data[index] = item;
  }

  fun push(x: T) {
    val capacity = this.capacity;
    if (this.size == capacity) {
      val newCapacity = if (capacity == 0) {
        1
      } else {
        capacity * 2
      };

      this.reserve(newCapacity);
    }

    this.data[this.size] = x;
    this.size += 1;
  }

  fun pop(): Option<T> {
    if (this.size == 0) {
      return None;
    }

    val toReturn = this.data[this.size - 1];
    this.size -= 1;

    return Some (toReturn);
  }
  
  fun map<U>(func: (T) -> U): Vec<U> {
    val vec: Vec<U> = Vec.newUninitialized(this.size);
    for (i in 0.to(this.size)) {
      vec[i] = func(this[i]);
    }

    return vec;
  }

  fun reserve(capacity: Int) {
    if (capacity <= this.capacity) {
      return;
    }

    val newData: Array<T> = Array.new(capacity);
    Array.copy(newData, 0, this.data, 0, this.size);
    this.data = newData;
    this.capacity = capacity;
  }

  fun resize(size: Int, value: T) {
    if (size > this.capacity) {
      this.reserve(size);
    }

    if (size > this.size) {
      for (i in this.size.to(size)) {
        this.data[i] = value;
      }
    }

    this.size = size;
  }

  fun isEmpty(): Bool = this.size == 0

  fun clone(): Vec<T> {
    val capacity = this.capacity;
    val data: Array<T> = Array.new(capacity);
    val size = this.size;

    Array.copy(data, 0, this.data, 0, this.size);

    return Vec { data, size, capacity };
  }

  fun slice(start: Int, end: Int): Vec<T> {
    if (start < 0 || end > this.size || end < start) {
      abort();
    }

    val size = end - start;
    val slice: Vec<T> = Vec { data: Array.new(size), size, capacity: size };
    Array.copy(slice.data, 0, this.data, start, size);

    return slice;
  }

  static fun copyRange<T>(src: Vec<T>, srcIndex: Int, dest: Vec<T>, destIndex: Int, count: Int) {
    if (count < 0 || srcIndex < 0 || destIndex < 0 ||
        srcIndex + count > src.size ||
        destIndex + count > dest.size) {
      abort();
    }

    Array.copy(src.data, srcIndex, dest.data, destIndex, count);
  }
}

/*
 * An iterator over a Vec. Contains both the Vec and the index of the next item to visit.
 */
type VecIterator<T> { data: Array<T>, size: Int, var index: Int }

methods Vec<T> {
  implements Iterable<T>

  override fun toIterator(): VecIterator<T> {
    val Vec { data, size, ... } = this;
    return VecIterator { data, size, index: 0 };
  }
}

methods VecIterator<T> {
  implements Iterator<T>

  override fun next(): Option<T> {
    if (this.index >= this.size) {
      return None;
    }

    val value = this.data[this.index];
    this.index += 1;

    return Some (value);
  }
}
